apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply plugin

//获取当前编译时间
def getCreateTime() {
    def createTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00")).trim()
    return createTime
}

/**
 * 获取Git 版本号
 */
def getGitSHA() {
    def gitSHA = 'git rev-parse --short HEAD'.execute().text.trim()
    return gitSHA
}

/**
 * 获取Git 提交次数
 */
def getGitCommitCount() {
    def gitCommitCount = 'git rev-list --count HEAD'.execute().text.trim();
    return gitCommitCount;
}
android {
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.jancar.radios"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode gitCommitCount.toInteger()
        versionName "v1.0."+gitSHA
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file("../platform.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        debug {
            storeFile file("../platform.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
//配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName
            if (variant.buildType.name.equals('release')) {
                fileName = "${applicationId}_release_${gitSHA}_${gitCommitCount}_${createTime}.apk"
            } else if (variant.buildType.name.equals('debug')) {
                fileName = "${applicationId}_debug_${gitSHA}_${gitCommitCount}_${createTime}.apk"
            }
            outputFileName = fileName;
        }
    }
}
greendao {
    schemaVersion 2
    daoPackage 'com.jancar.radio.greendao'
    targetGenDir 'src/main/java'
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compileOnly files('libs/mvp.jar')

    compile 'org.greenrobot:eventbus:3.0.0'
    implementation 'org.greenrobot:greendao:3.2.2'
    // add library
}
